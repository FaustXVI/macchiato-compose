allprojects {
    group = 'com.github.vdemeester'
    version = '0.1.0-SNAPSHOT'
    repositories {
        mavenCentral()
    }
}

subprojects { subproject ->

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

}

project(':macchiato-compose-core') {

    description = "Macchiato Compose Core"

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
            }
        }
    }

    configurations {
        integrationTestCompile.extendsFrom(testCompile)
        integrationTestRuntime.extendsFrom(testCompile)
    }
    dependencies {
        compile 'com.github.docker-java:docker-java:3.0.0-RC3'
        compile 'org.slf4j:slf4j-api:1.7.14'
        compile 'org.yaml:snakeyaml:1.17'
        compile 'ch.qos.logback:logback-classic:1.1.6'

        integrationTestCompile 'junit:junit:4.12'
        integrationTestCompile 'org.assertj:assertj-core:3.3.0'
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
        outputs.upToDateWhen { false }
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}